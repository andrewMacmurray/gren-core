module Test.Set exposing (tests)

import Basics exposing (..)
import Expect
import Array
import Set exposing (Set)
import Test exposing (..)


tests : Test
tests =
    describe "Set Tests"
        [ describe "empty" emptyTests
        , describe "singleton" singletonTests
        , describe "set" setTests
        , describe "remove" removeTests
        , describe "isEmpty" isEmptyTests
        , describe "member" memberTests
        , describe "size" sizeTests
        , describe "foldl" foldlTests
        , describe "foldr" foldrTests
        , describe "map" mapTests
        , describe "filter" filterTests
        , describe "partition" partitionTests
        , describe "union" unionTests
        , describe "intersect" intersectTests
        , describe "diff" diffTests
        , describe "toArray" toArrayTests
        , describe "fromArray" fromArrayTests
        ]



-- HELPERS


set42 : Set Int
set42 =
    Set.singleton 42


set1To100 : Set Int
set1To100 =
    Set.fromArray (Array.range 1 100)


set1To50 : Set Int
set1To50 =
    Set.fromArray (Array.range 1 50)


set51To100 : Set Int
set51To100 =
    Set.fromArray (Array.range 51 100)


set51To150 : Set Int
set51To150 =
    Set.fromArray (Array.range 51 150)


isLessThan51 : Int -> Bool
isLessThan51 n =
    n < 51



-- TESTS


emptyTests : Array Test
emptyTests =
    [ test "returns an empty set" <|
        \{} -> Expect.equal 0 (Set.size Set.empty)
    ]


singletonTests : Array Test
singletonTests =
    [ test "returns set with one element" <|
        \{} -> Expect.equal 1 (Set.size (Set.singleton 1))
    , test "contains given element" <|
        \{} -> Expect.equal True (Set.member 1 (Set.singleton 1))
    ]


setTests : Array Test
setTests =
    [ test "adds new element to empty set" <|
        \{} -> Expect.equal set42 (Set.set 42 Set.empty)
    , test "adds new element to a set of 100" <|
        \{} -> Expect.equal (Set.fromArray (Array.range 1 101)) (Set.set 101 set1To100)
    , test "leaves singleton set intact if it contains given element" <|
        \{} -> Expect.equal set42 (Set.set 42 set42)
    , test "leaves set of 100 intact if it contains given element" <|
        \{} -> Expect.equal set1To100 (Set.set 42 set1To100)
    ]


removeTests : Array Test
removeTests =
    [ test "removes element from singleton set" <|
        \{} -> Expect.equal Set.empty (Set.remove 42 set42)
    , test "removes element from set of 100" <|
        \{} -> Expect.equal (Set.fromArray (Array.range 1 99)) (Set.remove 100 set1To100)
    , test "leaves singleton set intact if it doesn't contain given element" <|
        \{} -> Expect.equal set42 (Set.remove -1 set42)
    , test "leaves set of 100 intact if it doesn't contain given element" <|
        \{} -> Expect.equal set1To100 (Set.remove -1 set1To100)
    ]


isEmptyTests : Array Test
isEmptyTests =
    [ test "returns True for empty set" <|
        \{} -> Expect.equal True (Set.isEmpty Set.empty)
    , test "returns False for singleton set" <|
        \{} -> Expect.equal False (Set.isEmpty set42)
    , test "returns False for set of 100" <|
        \{} -> Expect.equal False (Set.isEmpty set1To100)
    ]


memberTests : Array Test
memberTests =
    [ test "returns True when given element inside singleton set" <|
        \{} -> Expect.equal True (Set.member 42 set42)
    , test "returns True when given element inside set of 100" <|
        \{} -> Expect.equal True (Set.member 42 set1To100)
    , test "returns False for element not in singleton" <|
        \{} -> Expect.equal False (Set.member -1 set42)
    , test "returns False for element not in set of 100" <|
        \{} -> Expect.equal False (Set.member -1 set1To100)
    ]


sizeTests : Array Test
sizeTests =
    [ test "returns 0 for empty set" <|
        \{} -> Expect.equal 0 (Set.size Set.empty)
    , test "returns 1 for singleton set" <|
        \{} -> Expect.equal 1 (Set.size set42)
    , test "returns 100 for set of 100" <|
        \{} -> Expect.equal 100 (Set.size set1To100)
    ]


foldlTests : Array Test
foldlTests =
    [ test "with set and empty set acts as identity function" <|
        \{} -> Expect.equal set1To100 (Set.foldl Set.set Set.empty set1To100)
    , test "with counter and zero acts as size function" <|
        \{} -> Expect.equal 100 (Set.foldl (\_ count -> count + 1) 0 set1To100)
    , test "folds set elements from lowest to highest" <|
        \{} -> Expect.equal [ 3, 2, 1 ] (Set.foldl (\n ns -> Array.pushFirst n ns) [] (Set.fromArray [ 2, 1, 3 ]))
    ]


foldrTests : Array Test
foldrTests =
    [ test "with set and empty set acts as identity function" <|
        \{} -> Expect.equal set1To100 (Set.foldr Set.set Set.empty set1To100)
    , test "with counter and zero acts as size function" <|
        \{} -> Expect.equal 100 (Set.foldr (\_ count -> count + 1) 0 set1To100)
    , test "folds set elements from highest to lowest" <|
        \{} -> Expect.equal [ 1, 2, 3 ] (Set.foldr (\n ns -> Array.pushFirst n ns) [] (Set.fromArray [ 2, 1, 3 ]))
    ]


mapTests : Array Test
mapTests =
    [ test "applies given function to singleton element" <|
        \{} -> Expect.equal (Set.singleton 43) (Set.map ((+) 1) set42)
    , test "applies given function to each element" <|
        \{} -> Expect.equal (Set.fromArray (Array.range -100 -1)) (Set.map negate set1To100)
    ]


filterTests : Array Test
filterTests =
    [ test "with always True doesn't change anything" <|
        \{} -> Expect.equal set1To100 (Set.filter (always True) set1To100)
    , test "with always False returns empty set" <|
        \{} -> Expect.equal Set.empty (Set.filter (always False) set1To100)
    , test "simple filter" <|
        \{} -> Expect.equal set1To50 (Set.filter isLessThan51 set1To100)
    ]


partitionTests : Array Test
partitionTests =
    [ test "of empty set returns two empty sets" <|
        \{} -> Expect.equal { trues = Set.empty, falses = Set.empty } (Set.partition isLessThan51 Set.empty)
    , test "simple partition" <|
        \{} -> Expect.equal { trues = set1To50, falses = set51To100 } (Set.partition isLessThan51 set1To100)
    ]


unionTests : Array Test
unionTests =
    [ test "with empty set doesn't change anything" <|
        \{} -> Expect.equal set42 (Set.union set42 Set.empty)
    , test "with itself doesn't change anything" <|
        \{} -> Expect.equal set1To100 (Set.union set1To100 set1To100)
    , test "with subset doesn't change anything" <|
        \{} -> Expect.equal set1To100 (Set.union set1To100 set42)
    , test "with superset returns superset" <|
        \{} -> Expect.equal set1To100 (Set.union set42 set1To100)
    , test "contains elements of both singletons" <|
        \{} -> Expect.equal (Set.set 1 set42) (Set.union set42 (Set.singleton 1))
    , test "consists of elements from either set" <|
        \{} ->
            Set.union set1To100 set51To150
                |> Expect.equal (Set.fromArray (Array.range 1 150))
    ]


intersectTests : Array Test
intersectTests =
    [ test "with empty set returns empty set" <|
        \{} -> Expect.equal Set.empty (Set.intersect set42 Set.empty)
    , test "with itself doesn't change anything" <|
        \{} -> Expect.equal set1To100 (Set.intersect set1To100 set1To100)
    , test "with subset returns subset" <|
        \{} -> Expect.equal set42 (Set.intersect set1To100 set42)
    , test "with superset doesn't change anything" <|
        \{} -> Expect.equal set42 (Set.intersect set42 set1To100)
    , test "returns empty set given disjunctive sets" <|
        \{} -> Expect.equal Set.empty (Set.intersect set42 (Set.singleton 1))
    , test "consists of common elements only" <|
        \{} ->
            Set.intersect set1To100 set51To150
                |> Expect.equal set51To100
    ]


diffTests : Array Test
diffTests =
    [ test "with empty set doesn't change anything" <|
        \{} -> Expect.equal set42 (Set.diff set42 Set.empty)
    , test "with itself returns empty set" <|
        \{} -> Expect.equal Set.empty (Set.diff set1To100 set1To100)
    , test "with subset returns set without subset elements" <|
        \{} -> Expect.equal (Set.remove 42 set1To100) (Set.diff set1To100 set42)
    , test "with superset returns empty set" <|
        \{} -> Expect.equal Set.empty (Set.diff set42 set1To100)
    , test "doesn't change anything given disjunctive sets" <|
        \{} -> Expect.equal set42 (Set.diff set42 (Set.singleton 1))
    , test "only keeps values that don't appear in the second set" <|
        \{} ->
            Set.diff set1To100 set51To150
                |> Expect.equal set1To50
    ]


toArrayTests : Array Test
toArrayTests =
    [ test "returns empty list for empty set" <|
        \{} -> Expect.equal [] (Set.toArray Set.empty)
    , test "returns singleton list for singleton set" <|
        \{} -> Expect.equal [ 42 ] (Set.toArray set42)
    , test "returns sorted list of set elements" <|
        \{} -> Expect.equal (Array.range 1 100) (Set.toArray set1To100)
    ]


fromArrayTests : Array Test
fromArrayTests =
    [ test "returns empty set for empty list" <|
        \{} -> Expect.equal Set.empty (Set.fromArray [])
    , test "returns singleton set for singleton list" <|
        \{} -> Expect.equal set42 (Set.fromArray [ 42 ])
    , test "returns set with unique list elements" <|
        \{} -> Expect.equal set1To100 (Set.fromArray (Array.pushFirst 1 <| Array.range 1 100))
    ]
